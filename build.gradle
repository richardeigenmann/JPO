buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    }
}

plugins {
    id "org.sonarqube" version "3.0"
    id "com.github.ben-manes.versions" version "0.29.0"
    id 'signing'
    id 'java'
    id 'jacoco'
    //id 'org.beryx.jlink' version '2.16.4'
}

apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = 1.14
targetCompatibility = 1.14

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

compileJava {
    group = 'build'
    options.encoding = "UTF-8"
}
compileTestJava {
    group = 'build'
    options.encoding = "UTF-8"
}

test {
    // this tells the tests that we don't have a graphical environment TravisCI, SemaphoreCI etc need this.
    //systemProperty 'java.awt.headless', 'true'
    // copy the Gradle JVM properties down to the test JVM so that SemaphoreCI can ignore the tests that don't work in the headless environment
    systemProperties = System.getProperties()
}

javadoc {options.encoding = "UTF-8"}

task openJavadocInBrowser {
    dependsOn 'javadoc'
    group = 'documentation'
    doLast {
        java.awt.Desktop.desktop.open(new File(buildDir, "/docs/javadoc/index.html"))
    }
}

repositories {
    mavenCentral()
    jcenter()
}

def jpoVersion = '0.14'

dependencies {
    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'org.apache.directory.studio:org.apache.commons.io:2.4'
    implementation 'org.apache.commons:commons-jcs-core:2.2.1'
    implementation 'org.apache.commons:commons-jcs-jcache:2.2.1'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'commons-logging:commons-logging:1.2'
    implementation 'commons-net:commons-net:3.7'
    implementation 'commons-io:commons-io:2.7'
    implementation 'com.google.gdata:core:1.47.1'

    //implementation files('libs/gdata-maps-2.0.jar')
    //implementation files('libs/gdata-media-1.0.jar')
    //implementation files('libs/gdata-photos-2.0.jar')

    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'javax.mail:javax.mail-api:1.6.2'
    implementation 'com.jcraft:jsch:0.1.55'

    implementation files('libs/jwizz-0.1.4.jar')

    implementation 'org.jxmapviewer:jxmapviewer2:2.5'
    implementation 'com.drewnoakes:metadata-extractor:2.14.0'

    implementation 'com.miglayout:miglayout-swing:5.2'

    // needed to pull down 1.1.3p1 from 5 May 2018 from http://www.docking-frames.org/download.html
    // Maven dependencies don't work
    //implementation group: 'org.dockingframes', name: 'docking-frames-core', version: '1.1.2p6a'
    //implementation group: 'org.dockingframes', name: 'docking-frames-common', version: '1.1.2p6a'
    implementation files('libs/docking-frames-common.jar')
    implementation files('libs/docking-frames-core.jar')

    implementation files('libs/TagCloud.jar')
    
    //used by Metadata Extractor. Note only 5.x seems to be compatible
    //implementation 'com.adobe.xmp:xmpcore:5.1.3'
    //implementation 'com.adobe.xmp:xmpcore:6.1.10'
    implementation 'org.jetbrains:annotations:20.1.0'

    implementation 'com.twelvemonkeys.imageio:imageio-batik:3.6'
    implementation 'org.apache.xmlgraphics:batik-transcoder:1.13'
    implementation 'org.apache.xmlgraphics:batik-anim:1.13'
    implementation 'org.apache.xmlgraphics:batik-rasterizer-ext:1.13'
    implementation 'org.apache.xmlgraphics:batik-extension:1.13'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.4'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.13'
    implementation 'com.twelvemonkeys.imageio:imageio-bmp:3.6'
    //implementation 'com.twelvemonkeys.imageio:imageio-clippath:3.4.1' //OK -- But what does it do?
    implementation 'com.twelvemonkeys.imageio:imageio-icns:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-iff:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-hdr:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-pcx:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-pdf:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-pict:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-pnm:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-psd:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-sgi:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-tiff:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-tga:3.6'
    implementation 'com.twelvemonkeys.imageio:imageio-thumbsdb:3.6'
    //testImplementation 'junit:junit:4.13'
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.0-RC1')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.7.0-RC1')
    //testRuntime("org.junit.platform:junit-platform-runner:1.5.2")
    testImplementation('org.assertj:assertj-core:3.17.2')
    testImplementation('org.assertj:assertj-swing:3.9.2')

}
test {
    useJUnitPlatform()
}

sourceSets {
    main {
        resources {
            srcDir 'src/images'
            include '**/*.gif'
            include '**/*.jpg'
            
            srcDir 'src/dtd'
            include '**/*.dtd'
            include '**/*.txt'
            include '**/*.css'
            include '**/*.js'
                
            srcDir 'src/main/java/org/jpo/cache'
            include '**/*.ccf'
        }
    }
    test {
        main
        resources  {
            srcDir 'src/test/images'
            include '**/*.jpg'
            include '**/*.tiff'
            include '**/*.hdr'
            include '**/*.pdf'
            include '**/*.svg'
            include '**/*.bmp'
            include '**/*.pnm'
            include '**/*.sgi'
            include '**/*.tga'
            include '**/*.psd'
            include '**/*.ico'
            include '**/*.png'
            include '**/*.gif'
            include '**/*.iff'
            include '**/*.pcx'
            include '**/*.pct'
            include '**/*.icns'
            include '**/*.db'
        }
    }
}
    

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jar {
    manifest {
        attributes(
            "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '),
            "Main-Class": 'org.jpo.Main',
            'Permissions': 'all-permissions'
        )
        attributes(
            [ "Copy": "(c) 2001-2019, Richard Eigenmann, ZÃ¼rich, Switzerland" ], "Copyright"
        )
    }
    into ('org/jpo/images') { from 'src/images' }
}

// this task creates the FatJar with everything in it
shadowJar {
    dependsOn(build)
    into ('org/jpo/images') { from 'src/images' }
    mergeServiceFiles()
}

task signJar(dependsOn: 'shadowJar',description: 'Sign shadowJar.',group: 'build'){
    doLast{
        def props = new Properties()
        file("/richi/Privat/Documents/jpo.private.gradle.properties").withInputStream { props.load(it) }
        println 'Keystore file name: ' + props.getProperty("keystoreFileName")

        File signdir  = new File("$buildDir/jars/signed") as File
        signdir.mkdirs()

        ant.signjar(
            destDir: "${signdir.absolutePath}",
                  jar: 'build/libs/org.jpo-all.jar',
                  alias: props.getProperty("keyalias"),
                  storetype: "jks",
                  keystore: props.getProperty("keystoreFileName"),
                  storepass: props.getProperty("keystorepass"),
                  verbose: true,
                  preservelastmodified: "true",
                  tsaurl: "http://timestamp.digicert.com"
      )
    }
}

task buildWindowsExecutable(type: Exec) {
    group = 'build'
    dependsOn shadowJar
    File exedir  = new File("$buildDir/executables") as File
    exedir.mkdirs()
    commandLine '/usr/bin/makensis', '-XOutFile ' + exedir + '/Jpo.exe', 'Windowsbuilder/jpoexe.nsi'
}

task buildWindowsInstaller(type: Exec) {
    group = 'build'
    dependsOn buildWindowsExecutable
    File exedir  = new File("$buildDir/executables") as File
    workingDir exedir.toString()
    commandLine '/usr/bin/makensis', 
        '-XOutFile ' + exedir + '/JPO-Installer-0.14.exe',
        projectDir.absolutePath + '/Windowsbuilder/jpoinstaller.nsi'
}


task run(type: JavaExec) {
    group 'build'
    description 'Runs JPO from the shadowJar'
    dependsOn shadowJar
    classpath = files( 'build/libs/Jpo-all.jar' )
    main = 'org.jpo.Main'
}

jacocoTestReport {
    group 'verification'
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

task uploadWebsitePages(type:Exec) {
    group 'website'
    description 'Publishes the JPO website pages'
    dependsOn javadoc
    // note that the ssh key of sourceforge needs to be in /home/richi/.ssh/id_rsa
    // and that the known_hosts needs to have the host key ssh-keyscan -t rsa web.sourceforge.net
    commandLine ('rsync', '-avP',
            '--exclude="*"',
            '--include="*.php"',
            '--include="*.html"',
            '--include="*.css"',
            '--include="*.php"',
            '--include="fontconfig"',
            '--include="*.gif"',
            '--include="*.png"',
            '--include="*.jpg"',
            '--include="*.jnlp"',
            '--include="*.bat"',
            "${projectDir}/web", 'richieigenmann@web.sourceforge.net:/home/project-web/j-po/htdocs/')
}

task uploadJavadoc(type:Exec) {
    group 'website'
    description 'Publishes the JPO website pages'
    dependsOn javadoc
    // note that the ssh key of sourceforge needs to be in /home/richi/.ssh/id_rsa
    // and that the known_hosts needs to have the host key ssh-keyscan -t rsa web.sourceforge.net
    commandLine ('rsync', '-avP', "${buildDir}/docs/javadoc", 'richieigenmann@web.sourceforge.net:/home/project-web/j-po/htdocs/')
}

task uploadWindowsInstaller(type:Exec) {
    group 'website'
    description 'Publishes the JPO windows installer bundle'
    dependsOn buildWindowsInstaller
    // note that the ssh key of sourceforge needs to be in /home/richi/.ssh/id_rsa
    // and that the known_hosts needs to have the host key ssh-keyscan -t rsa web.sourceforge.net
    commandLine ('rsync', '-avP', "${buildDir}/executables/JPO-Installer-${jpoVersion}.exe", 'richieigenmann@web.sourceforge.net:/home/frs/project/j-po/')
}

task uploadWebsite() {
    group 'website'
    description 'Publishes the JPO website'
    dependsOn uploadWebsitePages, uploadJavadoc, uploadWindowsInstaller
}


allprojects { // Projects
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

/*jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher{
        name = 'hello'
        jvmArgs = ['-Dlog4j.configurationFile=./log4j2.xml']
    }
}*/